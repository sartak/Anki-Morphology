#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Morphology;

my $morph   = Anki::Morphology->new;
my $corpus  = $morph->corpus;
my $knowndb = $morph->knowndb;

my %i = map { $_ => 1 } $morph->fast_known_morphemes;

say "You KNEW " . keys(%i) . " words!";

$knowndb->begin_work;
    $knowndb->do("DELETE FROM morphemes;");
    $knowndb->do("DELETE FROM vars WHERE key = 'last_update';");
$knowndb->commit;

my %j = map { $_ => 1 } $morph->known_morphemes;

say "You KNOW " . keys(%j) . " words!";

say "Clearing corpus...";
my ($i, $e) = (0, 0);

my $morpheme_sth = $corpus->prepare("select morphemes from sentences where rowid=?");

$corpus->each_sentence('order by rowid asc;', sub {
    warn((2**$e++)."\n") if ++$i == 2**$e;

    my $sentence = shift;

    my $japanese = $sentence->japanese;

    my @new_morphemes = $morph->morphemes_of($japanese);
    my $new_morpheme_string = $corpus->morpheme_string_for($japanese, \@new_morphemes);

    $morpheme_sth->execute($sentence->rowid);
    my ($old_morpheme_string) = $morpheme_sth->fetchrow_array;

    return if $old_morpheme_string eq $new_morpheme_string;

    say "$old_morpheme_string -> $new_morpheme_string";

    $corpus->dbh->do("update sentences set morphemes=? where rowid=?", {}, $new_morpheme_string, $sentence->rowid);
    $sentence->refresh;
    $corpus->dbh->do("delete from morphemes where sentence=?", {}, $sentence->rowid);
    $sentence->insert_standalone_morphemes(\@new_morphemes);
});
