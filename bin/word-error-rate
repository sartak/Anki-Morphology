#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Morphology;

my $morph  = Anki::Morphology->new;
my $anki   = $morph->anki;

my %right_for;
my %wrong_for;
my %cards_for;

$anki->each_card(sub {
    my ($card) = @_;
    return unless $card->model->name eq '文';

    my $japanese = $card->field('日本語');
    my ($right, $wrong) = $anki->card_scores($card->id);

    for my $morpheme ($morph->morphemes_of($japanese)) {
        my $word = $morpheme->{dictionary};
        $right_for{$word} += $right;
        $wrong_for{$word} += $wrong;
        $cards_for{$word}++;
    }

});

my %right_for_card_count;
my %wrong_for_card_count;
my $max = 0;

for my $word (keys %cards_for) {
    my $count = $cards_for{ $word };
    $max = $count if $count > $max;

    $right_for_card_count{$count} += $right_for{$word};
    $wrong_for_card_count{$count} += $wrong_for{$word};
}

for my $count (1 .. $max) {
    next if !$right_for_card_count{$count};
    say "$count: " . ($right_for_card_count{$count} / ($right_for_card_count{$count} + $wrong_for_card_count{$count}));
}
