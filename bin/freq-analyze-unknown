#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Morphology;

my $morph  = Anki::Morphology->new;
my $anki   = $morph->anki;
my $corpus = $morph->corpus;

my %i = map { $_ => 1 } $morph->known_morphemes;
say "You know " . keys(%i) . " words!";

my %new_words;
my %f = $morph->morpheme_frequency(1);

my @sentences;
my %seen_sentence;

while (<>) {
    chomp;
    my $text = $_;
    next if $seen_sentence{$text}++;

    my @unknown;
    my $cost = 0;
    my %seen;

    for my $morpheme ($morph->morphemes_of($text)) {
        my $dict = $morpheme->{dictionary};
        next if $i{ $dict } || $seen{ $dict }++;

        $new_words{$dict}++;

        $text =~ s/(\Q$morpheme->{surface}\E)/\e[1;35m$1\e[m/g;
        push @unknown, $dict;
        $cost += 1 / ($f{$dict}||0.000001);
    }

    push @sentences, [$cost, $text, @unknown];
}

for my $sentence (sort { $a->[0] <=> $b->[0] } @sentences) {
    my $cost = shift @$sentence;
    my $text = shift @$sentence;
    say $text;
    for my $dict (sort { ($f{$b}||0) <=> ($f{$a}||0) } @$sentence) {
        say "  $dict: " . ($f{$dict}||0);
    }
    say "";
}

say "New words: " . scalar(keys %new_words);
